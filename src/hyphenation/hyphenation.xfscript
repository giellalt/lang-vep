! =============================== !
! Hyphenator for Vepsian
! =============================== !

! Still quite West Greenlandic !

echo << defining Cns and Vow... >>

define Vow	[ a | á | e | i | o | u | y | æ | ø | å | ä | ö |
                  A | Á | E | I | O | U | Y | Æ | Ø | Å | Ä | Ö | 
é | ó | ú | í | à | è | ò | ù | ì | ë | ü | ï | â | ê | ô | û | î | ã | ý |
É | Ó | Ú | Í | À | È | Ò | Ù | Ì | Ë | Ü | Ï | Â | Ê | Ô | Û | Î | Ã | Ý ] ;



define LongVow [ a a | A a | A A | e e | E e | E E | i i | I i | I I |
                 o o | O o | O O | u u | U u | U U | Ü ü | Ü Ü | a i | A i | A I |
                 ä ä | Ä ä | Ä Ä | ö ö | Ö ö | Ö Ö | y y | Y y | Y Y ] ;

define Diphth   [ e i | E i | ä i | Ä i | ö i | Ö i | 
                  a i | A i | o i | O i | u i | U i | ü i | Ü i | y i | Y i |
                  a u | A u | o u | O u | ä u | Ä u ] ;

define A2 [ a | A | i | I | u | U ] ;
define E2 [ e | E | i | I | u | U ] ;
define I2 [ i | I ] ;
define O2 [ o | O ] ;
define U2 [ u | U | i | I ] ;
define Y2 [ y | Y | i | I ] ;
define UE2 [ ü | Ü | i | I ] ;
define Ä2 [ ä | Ä | i | I ] ;
define Ö2 [ ö | Ö | i | I ] ;


define Cns [ b | c | č | d | đ | ð | f | g | h | j | k | l | m | n | ŋ | 
             B | C | Č | D | Đ | Ð | F | G | H | J | K | L | M | N | Ŋ | 
             ñ | p | q | r | s | š | t | ŧ | v | w | x | z | ž |   
             Ñ | P | Q | R | S | Š | T | Ŧ | V | W | X | Z | Ž ] ; 

define Pal [ %' ] ;

define S [ S | s ] ; 

define Cnss [ Palsymbol | Cns - S ] ;

define CnsPal [ Cns | Pal ] ;

!define Sgm [ Cns | Vow ] ;

define Dummy [ %^K ] ;

echo << Rules>>

define PalV        [..] -> %-  || Vow Cnss+ Pal (Cns)   _ Vow ; ! tal'v-kus
define beforeCV    [..] -> %-  || Vow             _ Cns Vow ;  !a-te-qu-seq
define sC          [..] -> %-  || Vow S Cns*      _ Cns (Pal) Vow ;
define Css         [..] -> %-  || Vow Cns S (Pal) _ S ;
define CCV         [..] -> %-  || Vow CnsPal+     _ Cns Vow ;  !il-lu, Matt-hæ-u-sip, Kris-tus
define CsCV        [..] -> %-  || Vow Cnss+       _ S Cnss Vow ; !Kron-stadt
define keep       [..] -> %^K ||  [a|A]           _ A2 ,
                                  [e|E]           _ E2 ,
                                  [i|I]           _ I2 ,
                                  [o|O]           _ O2 ,
                                  [ö|Ö]           _ Ö2 ,
                                  [y|Y]           _ Y2 ,
                                  [ü|Ü]           _ UE2 ,
                                  [ä|Ä]           _ Ä2 ,
                                  [u|U]           _ U2 ; 

define VV          [..] -> %-  || Vow             _ Vow ;      !Matt-hæ-u-sip

echo << Cleanup...>>
define clean  Dummy -> 0 ;


define Fixpal Pal -> %' ;

! Lagre denne fila: i xfst: source < dennefila.txt og deretter: save hy.fst
! cat fil.txt | preprocess | ./lookup -utf8 hy.fst | cut -f2 | tr '\n' ' ' | less

echo <<Combining...>>

read regex [
!.o. beforeCV
PalV
.o. CCV
.o. beforeCV
.o. sC
.o. Css
.o. CsCV
.o. keep
.o. VV
.o. clean
.o. Fixpal
] ;
