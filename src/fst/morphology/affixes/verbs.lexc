
!! # Verb inflection in Vepsian
! ---

!! ## Lexica from the *stems* file

!! ### Irregular verbs
! ei:e
LEXICON V_EI
!! @LEXNAME@ all forms of the *ei* verb
+Aux+Neg+Sg1:%>n K ;
+Aux+Neg+Sg2:%>d K ;
+Aux+Neg+Sg3:%>i K ;
+Aux+Neg+Pl1:%>m K ;
+Aux+Neg+Pl2:%>t K ;
+Aux+Neg+Pl3:%>i K ; !Is there any other variant

!! ### Regular verbs

LEXICON V_SUGIDA
!! @LEXNAME@ This and all V_... lexica redirect to *V-VowelStem, V-ConsonantStem, INDPRS3* (see below)
: V-VowelStem ;
: V-ConsonantStem ;
: INDPRS3 ;
INF_da ;
 PRFPRC_nu ;

LEXICON V_ANTTA
!! @LEXNAME@ antta:an
:da V-VowelStem ;
:d V-ConsonantStem ;
:da INDPRS3 ;
:t INF_ta ;
:d PRFPRC_nu ;

LEXICON V_OLDA
!! @LEXNAME@ (adding :le to vowstem)
:le V-VowelStem ;
:l V-ConsonantStem ;
+Ind+Prs+Sg3:%>m K ;
+Ind+Prs+Pl3:%>ba K ;
:l INF_da ;
:l PRFPRC_nu ;

LEXICON V_PESTA
!! @LEXNAME@ (adding -e) ti vowstem and indprs3)
:e V-VowelStem ;
: V-ConsonantStem ;
:e INDPRS3 ;
INF_ta ;
! PRFPRC_nu ;

LEXICON V_TEHTA ! 
!! @LEXNAME@ = tehta:te (adding :ge to vowst and indprs3)
:ge V-VowelStem-PRS ;
:g V-VowelStem-PRT ;
:g V-ConsonantStem_OTHER ;
 V-ConsonantStem_t ;
:ge INDPRS3 ;
:h INF_ta ;
:h PRFPRC_nu ;

LEXICON V_SIRTA !
!! @LEXNAME@ =  sirta:sir
:da V-VowelStem-PRS ;
:d V-VowelStem-PRT ;
:d V-ConsonantStem_OTHER ;
 V-ConsonantStem_t ;
:da INDPRS3 ;
! PRFPRC_nu ;

LEXICON V_JOKSTA ! 
!! @LEXNAME@ = joksta:joks
:e V-VowelStem-PRS ;
: V-VowelStem-PRT ;
 V-ConsonantStem ;
:e INDPRS3 ;
INF_ta ;
! PRFPRC_nu ;

LEXICON V_LETA ! 
!! @LEXNAME@ = leta:le
:nda V-VowelStem-PRS ;
:nd V-VowelStem-PRT ;
:nd V-ConsonantStem_OTHER ;
 V-ConsonantStem_t ;
:nda INDPRS3 ;
INF_ta ;
! PRFPRC_nu ;

LEXICON V_JODA ! 
!! @LEXNAME@ = joda:jo
: V-VowelStem-PRS ;
: V-VowelStem-PRT ;
 V-ConsonantStem ;
: INDPRS3 ;
 INF_da ;
 PRFPRC_nu ;


LEXICON V_VEDADA !
!! @LEXNAME@ =  vedada:ved
:a V-VowelStem-PRS ;
: V-VowelStem-PRT ;
:a V-ConsonantStem ; !CHECKME2019-07-25
:a INDPRS3 ;
:a INF_da ;
:a  PRFPRC_nu ;

LEXICON V_MAKSTA ! 
!! @LEXNAME@ = maksta:maks
:a V-VowelStem-PRS ;
:o V-VowelStem-PRT ;
 V-ConsonantStem ;
:a INDPRS3 ;
INF_ta ;
! PRFPRC_nu ;

LEXICON V_ASTTA ! 
!! @LEXNAME@ = astta:ast
:u V-VowelStem-PRS ;
:u V-VowelStem-PRT ;
 V-ConsonantStem ;
:u INDPRS3 ;
INF_ta ;
! PRFPRC_nu ;


LEXICON V_HUBETA ! 
!! @LEXNAME@ = hubeta:hube
:ne V-VowelStem-PRS ;
:n V-VowelStem-PRT ;
 V-ConsonantStem ;
:ne INDPRS3 ;
INF_ta ;
! PRFPRC_nu ;

LEXICON V_TULDA ! 
!! @LEXNAME@ = tulda:tul
:e V-VowelStem-PRS ;
: V-VowelStem-PRT ;
 V-ConsonantStem ;
:e INDPRS3 ;
INF_da ;
 PRFPRC_nu ;

LEXICON V_OTTA ! 
!! @LEXNAME@ = otta:ot
:a V-VowelStem-PRS ;
: V-VowelStem-PRT ;
 V-ConsonantStem ;
:a INDPRS3 ;
INF_ta ;
 PRFPRC_nu ;


LEXICON V_OIGETA !oigeta:oige
!! @LEXNAME@
:nda V-VowelStem-PRS ;
:nz V-VowelStem-PRT ;
 V-ConsonantStem ;
:nda INDPRS3 ;
INF_ta ;
:nda PRFPRC_nu ;

LEXICON V_VIRIGATA 
!! @LEXNAME@ =  virigata:viriga
:nde V-VowelStem-PRS ;
:nz V-VowelStem-PRT ;
 V-ConsonantStem ;
:nde INDPRS3 ;
INF_ta ;
! PRFPRC_nu ;

LEXICON V_KERATA ! 
!! @LEXNAME@ = kerata:kera
:da V-VowelStem-PRS ;
:z V-VowelStem-PRT ;
 V-ConsonantStem ;
:da INDPRS3 ;
INF_ta ;
! PRFPRC_nu ;

LEXICON V_JAGADA 
!! @LEXNAME@ =  jagada:jag (adding a to prt and a elsewhere)
:a V-VowelStem-PRS ;
:o V-VowelStem-PRT ;
:a V-ConsonantStem ;
:a INDPRS3 ;
:a INF_da ;
:a PRFPRC_nu ;

LEXICON V_TUGETA
!! @LEXNAME@ = tugeta:tuge
:da V-VowelStem ;
:%^DEVOICE V-ConsonantStem ;
:nda INDPRS3 ;
INF_ta ;
! PRFPRC_nu ;

LEXICON V_HOMAITA !
!! @LEXNAME@ =  homaita:homai
:če V-VowelStem-PRS ;
:č V-VowelStem-PRT ;
 V-ConsonantStem ;
:če INDPRS3 ;
INF_ta ;
:če PRFPRC_nu ;

!
LEXICON V_VALITA
!! @LEXNAME@  =  valita:vali
:če V-VowelStem ;
:%^DEVOICE V-ConsonantStem ;
:če INDPRS3 ;
INF_ta ;
! PRFPRC_nu ;

LEXICON V_AVAITA ! 
!! @LEXNAME@ = avaita:avai, avaidab avaiži
:da V-VowelStem-PRS ;
:ž V-VowelStem-PRT ;
 V-ConsonantStem ;
:da INDPRS3 ;
INF_ta ;
! PRFPRC_nu ;

!! ### Default lexicon

LEXICON V_
!! @LEXNAME@ default verb lexicon
 V-VowelStem ;
 V-ConsonantStem ;

!! ## The three contlex types

!! ### PNDPRS3

LEXICON INDPRS3
!! @LEXNAME@ for Sg3 and Pl3
+Ind+Prs+Sg3:%>b K ;
+Ind+Prs+Pl3:%>ba K ;



!! ### Vowel stems

LEXICON V-VowelStem
!! @LEXNAME@
V-VowelStem-PRS ;
V-VowelStem-PRT ;



LEXICON V-VowelStem-PRS
!! @LEXNAME@ This lexicon gives all  forms for vowel stems

!!!!!!!!!!!
!! #### Present Morphology
!!!!!!!!!!!
+Ind+Prs+Sg1:%>n K ;
+Ind+Prs+Sg2:%>d K ;
+Ind+Prs+Pl1:%>m K ;
+Ind+Prs+Pl2:%>t K ;

+Ind+ConNeg+Prs+Sg: K ;
+Ind+ConNeg+Prt+Sg:%>nd K ;

!!!!!!!!!!!!!!
!! #### IMPERATIVE 
!!!!!!!!!!!!!!

+Imprt+Sg2: K ;

!! #### present reflexive

+Refl+Ind+Prs+Sg1:%>moi K ;
+Refl+Ind+Prs+Sg2:%>toi K ;
+Refl+Ind+Prs+Sg3:%>se K ;
+Refl+Ind+Prs+Pl1:%>moiš K ;
+Refl+Ind+Prs+Pl2:%>toiš K ;
+Refl+Ind+Prs+Pl3:%>se K ;


LEXICON V-VowelStem-PRT
!!!!!!!!!!!!!
!! #### Preterite 
!!!!!!!!!!!!!

+Ind+Prt+Sg1:%>in K ;
+Ind+Prt+Sg2:%>id K ;
+Ind+Prt+Sg3:%>i K ;
+Ind+Prt+Pl1:%>im K ;
+Ind+Prt+Pl2:%>it K ;
+Ind+Prt+Pl3:%>iba K ;




!!!!!!!!!!!!!!!!!!!!!!!
!! #### conditional present 
!!!!!!!!!!!!!!!!!!!!!!!

+Cond+ConNeg+Prs:%>iži K ;

+Cond+Prs+Sg1:%>ižin K ;
+Cond+Prs+Sg2:%>ižid K ;
+Cond+Prs+Sg3:%>iži K ;
+Cond+Prs+Pl1:%>ižim K ;
+Cond+Prs+Pl2:%>ižit K ;
+Cond+Prs+Pl3:%>ižiba K ;

!!!!!!!!!!!!!
!! #### Imperfect Reflexive 
!!!!!!!!!!!!!

+Refl+Ind+Prt+Sg1:%>imoi K ;
+Refl+Ind+Prt+Sg2:%>itoi K ;
+Refl+Ind+Prt+Sg3:%>ihe K ;
+Refl+Ind+Prt+Pl1:%>imoiš K ;
+Refl+Ind+Prt+Pl2:%>itoiš K ;
+Refl+Ind+Prt+Pl3:%>ihe K ;

!!!!!!!!!!!!!!!!!!!!!!!
!! #### Conditional Present Reflexive
!!!!!!!!!!!!!!!!!!!!!!!

+Refl+Cond+Prs+Sg1:%>ižimoi K ;
+Refl+Cond+Prs+Sg2:%>ižitoi K ;
+Refl+Cond+Prs+Sg3:%>ižihe K ;
+Refl+Cond+Prs+Pl1:%>ižimoiš K ;
+Refl+Cond+Prs+Pl2:%>ižitoiš K ;
+Refl+Cond+Prs+Pl3:%>ižihe K ;

+Refl+Cond+Prs+ConNeg:%>ižihe K ;


!! ### Consonant Stems

LEXICON V-ConsonantStem
!! @LEXNAME@ V-ConsonantStem_OTHER ;
 V-ConsonantStem_t ;

LEXICON V-ConsonantStem_OTHER
!! @LEXNAME@ 
+Ind+ConNeg+Prs+Pl:%>koi K ;
+Ind+ConNeg+Prt+Pl:%>nugoi K ;
+PrfPrc:%>nu PRFPRC-DECLENSION ;
 PRFPRC_nu ;
 
!!!!!!!!!!!!!!
!! #### Imperative 
!!!!!!!!!!!!!!

+Imprt+Sg3:%>kaha K ;
+Imprt+Pl1:%>kam K ;
+Imprt+Pl2:%>kat K ;
+Imprt+Pl3:%>kaha K ;

!!!!!!!!!!!!!!!!!!!!!!!!!
!! #### Conditional Imperfect 
!!!!!!!!!!!!!!!!!!!!!!!!!

+Cond+Prt+Sg1:%>nuižin K ;
+Cond+Prt+Sg2:%>nuižid K ;
+Cond+Prt+Sg3:%>nuiži K ;
+Cond+Prt+Pl1:%>nuižim K ;
+Cond+Prt+Pl2:%>nuižit K ;
+Cond+Prt+Pl3:%>nuižiba K ;


!!!!!!!!!!!!!
!! #### Reflexive 
!!!!!!!!!!!!!

+Refl+Ind+Prs+ConNeg+Pl:%>koiš K ;
+Refl+Ind+Prt+ConNeg+Pl:%>nus K ;
+Refl+Ind+Prf+Pl:%>nus K ;

!!!!!!!!!!!!!!
!! #### Imperative 
!!!!!!!!!!!!!!

+Refl+Imprt+Sg3:%>kahas K ;
+Refl+Imprt+Pl1:%>kamoiš K ;
+Refl+Imprt+Pl2:%>katoiš K ;
+Refl+Imprt+Pl3:%>kahas K ;
+Refl+Imprt+Sg3:%>koiš K ;
+Refl+Imprt+Pl:%>koiš K ;

LEXICON V-ConsonantStem_t
!! @LEXNAME@ 

+Refl+Ind+Prs+ConNeg+Sg:%>te K ;
+Refl+Imprt+Sg2:%>te K ;
+Refl+Imprt+Sg2:%>te K ;
+Pss+PrfPrs:%>tud K ;
!+Der/Tu+Ptc+Pss+Pst:%>tu K ; ! This will have to be AdjTag or something
INF_ta ;
+Inf+Refl:%>tas K ;
+Pss+Prs:%>ta K ;
+Pss+Prt:%>ti K ;
+Inf+Der/Te+Ine:%>tes K ;
+Inf+Der/Te+EssInst:%>ten K ;

+Der/IA+Act+PrsPrc:%>jA1 K ; ! This will have to be AdjTag or something
+Inf+Der/ma+Ill:%>mha K ;
+Inf+Der/ma+Ine:%>mas K ;
+Inf+Der/ma+Ela:%>maspäi K ;
+Inf+Der/ma+Ade:%>mal K ;
+Der+Der/Mine+N:%>mi N_TOSHTMINE ;
+Der+Der/Matoi+A:%>mato A_KENGATOI ;

LEXICON PRFPRC_nu
+PrfPrc+Sg:%>nu K ;
+PrfPrc+Pl:%>nuded K ;
+Cond+ConNeg+Prt:%>nuiži K ;


LEXICON INF_da
+Inf:%>da K ;

LEXICON INF_ta
+Inf:%>ta K ;


! vim: set ft=xfst-lexc:
