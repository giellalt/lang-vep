!! # Pronouns

!! Veps pronouns ...

LEXICON PRONOUN-TYPES
!! @LEXNAME@ split in types: prs, interr, neg, dem.
 PERS ;
 INTERR ;
! INDEFINITE-PRON ;
 NEG-PRON ;
! COLL-PRON ;
  DEM-PRON ;
ičeze+Pron+Sg3+Gen:ičeze K ;
ičeze+Pron+Pl3+Gen:ičeze K ;
ičeze+Pron+Sg3+Apr2:ičezennoks K ;
ičeze+Pron+Pl3+Apr2:ičezennoks K ;!Do we want + OR +SP3

LEXICON PERS
!! @LEXNAME@  split in persons
minä+Pron:min PersSg1 ;
sinä+Pron:sin PersSg2 ;
hän+Pron:hä PersSg3 ;
minä+Pron:m PersPl1 ;
sinä+Pron:t PersPl2 ;
hän+Pron:h PersPl3 ;

LEXICON PersSg1
!! @LEXNAME@ split in Nom, Gen, Acc
+Pers+Sg1:ä NOM-SUF ;
+Pers+Sg1:u GEN-SUF ;
+Pers+Sg1:u ACC-SUF ;!is this distinguishable at morphosyntactic level better than later? 20230620
+Pers+Sg1:u ELA-SUF ;
+Pers+Sg1:u ILL-SUF ;
+Pers+Sg1:u INE-SUF ;
+Pers+Sg1:u ALL-SUF ;
+Pers+Sg1:u ADE-SUF ;
+Pers+Sg1:u ABL-SUF ;
+Pers+Sg1:u COM-SUF ; !minunke
+Pers+Sg1:u APR2-SUF ; ! minunnoks

LEXICON PersSg2
!! @LEXNAME@ split in Nom, Gen, Acc
+Pers+Sg2+Nom:ä NOM-SUF ;
+Pers+Sg2+Gen:u GEN-SUF ;
+Pers+Sg2+Gen:u ACC-SUF ;
+Pers+Sg2:u ELA-SUF ;
+Pers+Sg2:u ILL-SUF ;
+Pers+Sg2:u INE-SUF ;
+Pers+Sg2:u ALL-SUF ;
+Pers+Sg2:u ADE-SUF ;
+Pers+Sg2:u ABL-SUF ;
+Pers+Sg2:u COM-SUF ; !minunke
+Pers+Sg2:u APR2-SUF ; ! minunnoks

LEXICON PersSg3
!! @LEXNAME@ split in Nom, Gen, Acc, Par and 10 other cases
+Pers+Sg3:n NOM-SUF ;
+Pers+Sg3:ne GEN-SUF ;
+Pers+Sg3:ne ACC-SUF ;
+Pers+Sg3:n PAR-SUF ;
+Pers+Sg3+Par:ndast K ;!2024-04-02
+Pers+Sg3:ne ABE-SUF ;
+Pers+Sg3:ne ILL-SUF ;
+Pers+Sg3:ne INE-SUF ;
+Pers+Sg3:ne ELA-SUF ;
+Pers+Sg3:ne ALL-SUF ;
+Pers+Sg3:ne ADE-SUF ;
+Pers+Sg3:ne ABL-SUF ;
+Pers+Sg3:ne COM-SUF ; !hänenke
+Pers+Sg3:ne APR1-SUF ; !hänenno
+Pers+Sg3:ne APR2-SUF ; !hänennoks

LEXICON PersPl1
!! @LEXNAME@ split in Nom and obl
+Pers+Pl1+Nom:ö K ;
+Pers+Pl1:e PERS-PL_01 ;

LEXICON PersPl2
!! @LEXNAME@ split in Nom and obl
+Pers+Pl2+Nom:ö K ;
+Pers+Pl2:e PERS-PL_01 ;

LEXICON PersPl3
!! @LEXNAME@ split in Nom and obl
+Pers+Pl3+Nom:ö K ;
+Pers+Pl3:e PERS-PL_01 ;

LEXICON PERS-PL_01
!! @LEXNAME@ add case suffix
+Abl:%>ilpäi K ;
+Add1:%>ihepäi K ;
+Ade:%>il K ;
+All:%>ile K ;
+Ela:%>išpäi K ;
+Gen:%>iden K ;
+Ill:%>ihe K ;
+Ine:%>iš K ;
+Par:%>id K ;
+Par:%>itä K ;!CHECKME 20230620 Bible
:%>ide COM-SUF ;
:%>ide APR1-SUF ;
:%>ide APR2-SUF ;
:%>ide EGR-SUF ;!heidennopäi

LEXICON INTERR
!! @LEXNAME@ add case suff
mi+Pron+Interr+Sg+Nom:mi K ;
mi+Pron+Interr+Sg+Par:midä K ;
mi+Pron+Interr+Sg+Gen:min K ;
mi+Pron+Interr+Sg+Acc:min K ;
mi+Pron+Interr+Sg+Ade:mil K ;
mi+Pron+Interr+Sg+Ine:miš K ;
mi+Pron+Interr+Sg+Ela:mišpäi K ;
mi+Pron+Interr+Sg+Ill:mihe K ;
mi+Pron+Interr+Sg+Tra:mikš K ;
ken+Pron+Interr+Sg+Nom:ken K ;
ken+Pron+Interr+Sg+Gen:kenen K ;
ken+Pron+Interr+Sg+Par:keda K ;

LEXICON DEM-PRON
!! @LEXNAME@ split in Nom Gen par
se+Pron+Dem+Sg+Nom:se K ;
se+Pron+Dem+Sg+Gen:sen K ;
se+Pron+Dem+Sg+Par:sidä K ;

LEXICON NEG-PRON
!! @LEXNAME@ split in Nom Gen Par
niken+Pron+Neg+Sg+Nom:niken K ;
niken+Pron+Neg+Sg+Gen:nikenen K ;
niken+Pron+Neg+Sg+Par:nikeda K ;

! vim: set ft=xfst-lexc:
