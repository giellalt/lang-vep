! =================================== 
!! # The Veps morphophonological/twolc rules file 
! =================================== 

!! This file documents the [phonology.twolc file](http://github.com/giellalt/lang-vep/blob/main/src/fst/phonology.twolc) 


!! ## Alphabet, sets, definitions

Alphabet
!! ### Alphabet
!! #### The letters
a b d e f g h i j k l m n o p r s š t u v z ž ü ä ö !!= * @CODE@ 
A B D E F G H I J K L M N O P R S Š T U V Z Ž Ü Ä Ö !!= * @CODE@ 
š ž y !!= * @CODE@ 
Š Ž Y !!= * @CODE@ 
ʼ !!= * @CODE@ = U+02BC MODIFIER LETTER APOSTROPHE
' !!= * @CODE@ = U+0027  APOSTROPHE

!! #### Archiphonemes
%{eØ%}:e !!= * @CODE@  vowel loss in oiged:oiktan
%{uØ%}:u !!= * @CODE@  vowel loss in sapug:sapkan
%{iØ%}:i !!= * @CODE@  vowel loss in paltin:paltnan
%{aØ%}:a !!= * @CODE@  vowel loss in samal:samlan
%{oØ%}:o !!= * @CODE@  vowel loss in zerkol:zerklon
AÄ1:a !!= * @CODE@  Vowel harmony with "(t)a/ä"
AÄ1:ä !!= * @CODE@  Vowel harmony with "(t)a/ä"
AÄ1:0 !!= * @CODE@  Vowel harmony with 0

! Vowel harmony with "loi/löi" also kalatOÖ1in
OÖ1:o  !!= * @CODE@ 
OÖ1:ö  !!= * @CODE@ 

! Vowel harmony with "ttu/ttü"
UY1:u  !!= * @CODE@ 
UY1:ü  !!= * @CODE@ 

! this appears in the illative
V1:a    !!= * @CODE@ 
V1:e    !!= * @CODE@ 
V1:i    !!= * @CODE@ 
V1:o    !!= * @CODE@ 
V1:u    !!= * @CODE@ 
V1:ü    !!= * @CODE@ 
V1:ä    !!= * @CODE@ 
V1:ö    !!= * @CODE@ 

! These appear with the inessive and adessive
V2:a   !!= * @CODE@ 
V2:e   !!= * @CODE@ 
V2:i   !!= * @CODE@ 
V2:o   !!= * @CODE@ 
V2:u   !!= * @CODE@ 
V2:ü   !!= * @CODE@ 
V2:ä   !!= * @CODE@ 
V2:ö   !!= * @CODE@ 

! These reduplicate the preceding vowel if it in turn is preceded by a consonant
V3:a   !!= * @CODE@ 
V3:e   !!= * @CODE@ 
V3:i   !!= * @CODE@ 
V3:o   !!= * @CODE@ 
V3:u   !!= * @CODE@ 
V3:ü   !!= * @CODE@ 
V3:ä   !!= * @CODE@ 
V3:ö   !!= * @CODE@ 
V3:0   !!= * @CODE@ 

! These are for developing underlying morphology rules:
! SNM: Symbols copied from root.lexc, not earlier defined; they need proper
! rules to be realised (or not) depending on context, and with correct
! realisation.
%^RmVow:0 !!= * @CODE@ for removing vowels
 AO1:0  !!= * @CODE@ 
  A1:0  !!= * @CODE@ 
  D1:0  !!= * @CODE@  
  E1:0  !!= * @CODE@ 
 EH1:0  !!= * @CODE@ 
 EI1:0  !!= * @CODE@ 
  I1:0  !!= * @CODE@ 
INE1:0  !!= * @CODE@ 
QAO1:0  !!= * @CODE@  
QAO1:a  !!= * @CODE@ 
QAQ1:0  !!= * @CODE@ 
QEQ1:0  !!= * @CODE@ 
QÄQ1:0  !!= * @CODE@ 
  U1:0  !!= * @CODE@ 
 ZD1:0  !!= * @CODE@ 
 ZD1:0  !!= * @CODE@ 
 ZS1:0  !!= * @CODE@ 
 ZS1:0  !!= * @CODE@ 

! Veps 
 %^DEVOICE:0   !!= * @CODE@  vezi:vet; pen’:pen’t
 %^PEN:0       !!= * @CODE@  Control final vs penultimate

! this k is not effected by gradation
K1:k   !!= * @CODE@ 
%^NoGrad:0   !!= * @CODE@ This will be placed after a stem to break Gradation

! this weakens the stem _ompel_ to _ommel_
%^WGStem:0   !!= * @CODE@ 

! The ti => si
%^TS:0   !!= * @CODE@ 

! Vowel raising
%^RVow:0  !!= * @CODE@ 

%-    !!= * @CODE@ Hyphen in  constructions 
%>  !!= * @CODE@ 
#     !!= * @CODE@  Word boundary for both lexicalised and dynamic compounds

Cx  !!= * @CODE@ these are probably not needed
Cy  !!= * @CODE@ 
X  !!= * @CODE@ 
Y  !!= * @CODE@ 

!! #### Triggers
%^LVow:0		   !!= * @CODE@ 
%^LCns:0		   !!= * @CODE@ 
%^WCns:0		   !!= * @CODE@ 
%^AtoO:0		   !!= * @CODE@ 
%^ÄtoÖ:0		   !!= * @CODE@ 
%^OddSyll:0		   !!= * @CODE@ 
%^StretchSyll2:0   !!= * @CODE@ 
%^SyllBr:0		   !!= * @CODE@ 
%^E1:0			   !!= * @CODE@ 

! Literal quotes and angles must be escaped (cf morpheme boundaries further down):
 »7     ! »
 «7     ! «
 %[%>%] ! >
 %[%<%] ! <
 #7     ! #


;


!! ### sets
Sets

VowBack = a o u ;						    !!≈ * @CODE@  
VowFront = ä ö y ü ;					    !!≈ * @CODE@  
VowNeutral = e i ; 						    !!≈ * @CODE@  
VowNonHigh = a o ä ö e ;				    !!≈ * @CODE@  
Vow = a o u ä ö y ü e i ;				    !!≈ * @CODE@  
Cns = b d f g h j k l m n p r s š t v z ž ; !!≈ * @CODE@  
CnsVoiceless = f h k p s š t ;			    !!≈ * @CODE@  
Letters = Vow Cns ;						    !!≈ * @CODE@  



!! ### Definitions
Definitions

!! Front Trigger
FT =  [ # Cns* [ VowFront | VowNeutral | i: ] | :VowFront ] \[ # | VowBack: | :VowBack ]*  ;


!! Back Trigger 
BT =   ( # Cns* ( [ VowNeutral | i: ] \[ # | VowFront: | :VowFront ]* ) ) [ VowBack: | :VowBack ]  \[ # | VowFront: | :VowFront ]* ;

! Gradation strong to weak
!GS2W = [ p:v | m p:m | t:0 | l t:l | r t:r | n t:n | s t:s | k:0 | l k:0 | r k:0 | n k:g | s k:0 ] ;

!! Short vowel
SV = [ [ # | Cns+ ] :Vow ] ;

 ! If a vowel is preceeded by this, is can be long
!  in both the inessive and the adessive
!! Inessive lengthening of vowel
IL = [ # Cns* Vow : Cns:+ | Cns* Vow+ Cns:+  Vow:+ Cns:+  ] ;

!! Right context for gradation
GRC =  [ [ ( %> ) AÄ1: | OÖ1: | UY1: | Vow: | :Vow ]  ( %> i ) |  %>  i ] %> %^WCns:0  ;

! oma:ommaa, 
! Subsequent Long Vowel
! preceeded by "Cns _"
! 2012-10-11 SLV =  [ :Vow [ ( %> %^LCns:0 %^LVow:0 | %> %^LCns:0 | %> i %> | %> ) [ V1:Vow | AÄ1: ] |  0:Vow %> ( %^LCns:0 | %^WCns: ) %^LVow: | %> %^LCns:0  ] ] ;


!! ## Rules
Rules


!
!! ### Vowel change 
!

! Stem vowel loss before preterite and plural "i"
"StemVowLoss before _i_"
!! **RULE: @RULENAME@** = 
!! **@RULENAME@**  
 e:0 <=> # Cns* Vow+ Cns+ _  %> i  ;
         Cns: _ Cns: (%^PEN: %^DEVOICE:) %^RmVow: ;
!!€ oiged^PEN^DEVOICE^RmVow^DEVOICEa>n
!!€ oik0t0000a>n

"Stem-internal vowel loss"
!! **RULE: @RULENAME@** = 
!! **@RULENAME@**
 Vx:0 <=> _ Cns: %^RmVow: ;
              where Vx in (%{aØ%} %{iØ%} %{uØ%} %{eØ%} %{oØ%} ) ;


"QAO1 Sg Rule"
!! **RULE: @RULENAME@** = 
QAO1:a <=> _ ( %^DEVOICE: ) %> Cns ;

"QAO1 Pl Rule"
!! **RULE: @RULENAME@** = 
QAO1:o <=> _ ( %^DEVOICE: ) %> :i ( %^DEVOICE: ) Cns ;

"V1 Illative rule"
V1:Vx <=> Vx: Cns:* %> h: _ ;
      where Vx in ( a e i o u ü ä ö ) ; 

!!€ nado>hV1
!!€ nado>ho

!!€ marja>hV1
!!€ marja>ha

"vowel loss"
!! **@RULENAME@** vauged: vauktan
 Vx:Vy <=> Cns: _ (%^PEN: %^DEVOICE:) %^RmVow: ;
              where Vx in (u a)
	            Vy in (0 0) matched ;

!! ### Consonant change 

"devoicing of adjecent stops"
!! **@RULENAME@** vauged: vauktan
 Cx:Cy <=>  _ [%{eØ%}: |%{uØ%}: ] Cns:CnsVoiceless %^RmVow: %^DEVOICE: %> ;
            Cns:CnsVoiceless [ %{eØ%}: | %{uØ%}: ] _ %^RmVow: %^DEVOICE: %> ;
	    [ l (ʼ) | m | n | r | Vow:] _ (%^PEN: %^DEVOICE:) (%^RmVow:) %^DEVOICE: ;
	    [ l (ʼ) | m | n | r | Vow:] _ Vow: Cns: %^PEN: %^DEVOICE: ;
              where Cx in (g d b)
                    Cy in (k t p) matched ;
!! oiged+A+Sg+Gen: __right/oikea__
!!€ oiged^PEN^DEVOICE^RmVow^DEVOICEa>n
!!€ oik0t0000a>n

"devoice fricatives z:s"
!! **@RULENAME@**
Cx:Cy <=> Vow _ %^DEVOICE: ;
      	  where Cx in (z ž)
	  	Cy in (s š) matched ;
!! pesta+V+Inf: __wash/pestä__
!!€ pez^DEVOICE>ta
!!€ pes0>ta


"d:z in vodes, voziš"
!! **@RULENAME@**  
d:z <=> [ n | l | r | Vow ] _  %^TS:0  ; ! GRC ;
! last-minute verbs
! "ti:si in verbs"
!   [ n | l | r | Vow Vow ] _ %^TS:0 ; !  pittää: piti 


!"d:t in voden, vot"
!!! **@RULENAME@**  
! d:t <=> [ d: | ( n | l | r ) ’ | z: ] %^DEVOICE:0 %> _ [ # | %- ] ;

!"d:0 in voden, vot"
!!! **@RULENAME@**  
! d:0 <=> _ %^DEVOICE:0 %> d: ;

!"z:s in meletuz:meletust"
!!! **@RULENAME@**  
! z:s <=> _ %^DEVOICE:0 ;

"’:0 before front vowels"
!! **@RULENAME@**  
 ’:0 <=> _  %>  [VowFront | VowNeutral ] ;

