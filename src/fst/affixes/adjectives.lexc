!! # Adjective inflection
!   ---------------------


! LEXICON Comparisons
! +A+pos:0        Cases   ;
! +A+cmp:er       Cases   ;
! +A+sup:est      Cases   ;


!! ## Temporary lexicon


LEXICON A_  !!≈ * **@CODE@** = , when we do not know. Redirecting to N_ (?) 
 N_ ;


!! ## Regolar lexica

LEXICON A_UZ !!≈ * **@CODE@** = uz’:ud, goes to NMN_NORUZ
: NMN_NORUZ ;

LEXICON A_RUSKED !!≈ * **@CODE@** = gets +A and goes to NMN\_RUSKED
+A: NMN_RUSKED ;

LEXICON A_MUNA  !!≈ **@CODE@**   muna:mun
 N_MUNA ;
+Comp:e%>mb N_POIG ;

LEXICON A_NADO  !!≈ * **@CODE@** =  nado:nado
 N_NADO ;
+Comp:%>mb N_POIG ;


LEXICON A_POIG  !!≈ * **@CODE@** =  poig:poig
 N_POIG ;
+Comp:e%>mb N_POIG ;

LEXICON A_MARJ  !!≈ * **@CODE@** =  marj:marj
 N_MARJ ;
+Comp:e%>mb N_POIG ;

LEXICON A_JAUH  !!≈ * **@CODE@** =  jauh:jauh
!(-on\,\ -oid\)
 N_JAUH ;
+Comp:%>mb N_POIG ;

LEXICON A_OIGED  !!≈ * **@CODE@** = oiged:oig%{eØ%}d
!oi|ged (-ktan, -ktoid)
 N_OIGED ;
+Comp:%^RmVow%^DEVOICE%>e%>mb N_POIG ;

LEXICON A_KEL  !!≈ * **@CODE@** = kel':kel
 N_KEL ;
+Comp:%>e%>mb N_POIG ;
+Der+Der/Uz1+N:ud N_NORUZ ;

LEXICON A_SAR1  !!≈ * **@CODE@** = sar':sar
 N_SAR1 ;
+Comp:%>e%>mb N_POIG ;
+Der+Der/Uz1+N:ud N_NORUZ ;

LEXICON A_KEZI  !!≈ * **@CODE@** = kezi:ked
 N_KEZI ;
+Comp:%>e%>mb N_POIG ;
+Der+Der/Uz1+N:ud N_NORUZ ;


LEXICON A_VIDENZ1  !!≈ * **@CODE@** =  videnz':viden (-den, -t, -zid)
N_VIDENZ1 ;


LEXICON A_LANDEH  !!≈ * **@CODE@** =  landeh:land
 N_LANDEH ;
+Comp:%>he%>mb N_POIG ;

LEXICON A_VEDEKAZ  !!≈ * **@CODE@** =  vedekaz:vedeka
 N_VEDEKAZ ;
+Comp:ha%>mb N_POIG ;

LEXICON A_SEIBAZ  !!≈ * **@CODE@** =  seibaz:seib
 N_SEIBAZ ;
+Comp:ha%>mb N_POIG ;

LEXICON A_CIPUINE  !!≈ * **@CODE@** = cipuine:cipu
 N_CIPUINE ;
+Comp:iže%>mb N_POIG ;
+Der+Der/Uz1+N:ižud N_NORUZ ;

LEXICON A_TOSHTMINE  !!≈ * **@CODE@** =  toštmine:toštmi
 N_TOSHTMINE ;
+Comp:že%>mb N_POIG ;

LEXICON A_KENGATOI  !!≈ * **@CODE@** =  kengätoi:kengäto
+Comp:ma%>mb N_POIG ;
+Der+Der/Uz1+N:mud N_NORUZ ;
:i SG-NOM-SUF ;
:n SG-PAR-SUF_t ;
:ma SG-OBLIQUE ;
! Plural
:ma PL-NOM-SUF ;
:ma PL-ACC-SUF ;
:m PL-OBLIQUE ;

LEXICON PRFPRC-DECLENSION  !!≈ * **@CODE@** =  -nu
+Comp:%>mb N_POIG ;
 SG-NOM-SUF ;
: SG-PAR-SUF_t ;
:de SG-OBLIQUE ;
! Plural
:de PL-NOM-SUF ;
:de PL-ACC-SUF ;
:z  PL-OBLIQUE ;


! vim: set ft=xfst-lexc:

